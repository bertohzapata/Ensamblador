ASSUME CS:CODIGO, DS:DATOS, SS:PILA

;SUCESIÓN FIBONACCI: (Realizar prueba de escritorio para observar el desarrollo del código. La lógica de este programa está en el cuaderno)

;A TENER EN CUENTA:	*EL VALOR DE INGRESO DE N DEBE SER MAYOR O IGUAL A 2.
;					*EL VALOR MÁXIMO DE N DEBE SER 23, DE LO CONTRARIO EL RESULTADO SE PASARÁ DE 2^16 Y NO SE IMPRIMIRÁ.
;					*EL USO DE LA PILA Y CÓMO SE ALMACENAN DATOS EN ELLA.
;					*SE HACE DIRECCIONAMIENTO INDIRECTO EN LA PILA MEDIANTE EL REGISTRO BP (LÍNEA 24).
;					*EL CICLO FOR (LOOP) EMPIEZA EN CERO Y TERMINA EN (VALOR_INGRESADO-1) (LÍNEA 22).
;					*EL USO DE OTRAS SUBRUTINAS Y SUS RESPECTIVAS VARIABLES EN EL DS.

CODIGO SEGMENT	
	MOV AX,DATOS
	MOV DS,AX
	
	MOV AX, 1				;DOS PRIMEROS VALORES INICIALES VAN A LA PILA.
	MOV BX, 1
	PUSH AX
	PUSH BX
	CALL LECTURA_NUMERO		;INGRESO POR TECLADO DEL VALOR DE N.
	MOV CX, AX				;AX CORRESPONDE A N, VALOR EL CUAL SE PASA A CX PARA HACER EL CICLO.
	DEC CX					;EL CICLO FOR VA DE 0 A (N-1)
CICLO:
	MOV BP, SP				;SE HACE DIRECCIONAMIENTO INDIRECTO EN LA PILA MEDIANTE EL REGISTRO BP.
	MOV AX, [BP+2]			;RECORDAR QUE LA PILA VA HACIA DIRECCIONES BAJAS DE MEMORIA, POR ESO SE SUMA (PARA SUBIR), Y SE MUEVE DE 2 EN 2.
	MOV BX, [BP]
	ADD AX, BX
	PUSH AX
	LOOP CICLO
	POP AX					;SE SACA DE LA PILA A AX EL ÚLTIMO NÚMERO QUE SE LE INGRESÓ Y ESTE ES EL RESULTADO QUE SE DEBE IMPRIMIR.
	CALL IMPRIMIR_DECIMAL
	MOV AH, 4CH
	INT 21H
		
				;SUBRUTINA PARA EL INGRESO POR TECLADO DE UN NÚMERO DE 2 DÍGITOS: (revisar archivo LECTNUM.ASM)
				LECTURA_NUMERO PROC
					;BREVE MENSAJE:
					MOV AH, 09H
					LEA DX, MENSAJE
					INT 21H
					;INGRESO DE LOS DÍGITOS DEL NÚMERO:
				LECTNUM2:
					MOV AH, 01H
					INT 21H
					MOV AH, 0H			;PARA QUE QUE NO INTERFIERA EL SERVICIO 01H EN EL REGISTRO AX.
					CMP AL, 0DH			;EL INGRESO DE DÍGITOS TERMINA CUANDO SE HALLA PRESIONADO LA TECLA ENTER O 
					JE LECTNUM1				;SE HALLA LLEGADO AL TOPE, EL CUAL ES UN NÚMERO DE 5 DÍGITOS.
					PUSH AX
					INC N				;N ES UN CONTADOR QUE LLEVA LA CUENTA DE LOS DÍGITOS QUE SE HAN INGRESADO A 
					CMP N, 2H				;LA PILA, ADEMÁS DETERMINA EL TIPO DE NÚMERO A PARTIR DE SUS DÍGITOS. SI N=5, QUIERE 
					JE LECTNUM1				;DECIR QUE SE HA LLEGADO AL TOPE Y SE HA INGRESADO UN NÚMERO DE 5 DÍGITOS.
					JMP LECTNUM2
					;DETERMINACIÓN DEL NÚMERO INGRESADO A PARTIR DE SUS DÍGITOS:
				LECTNUM1:
					CMP N, 01H 			;PARA UN NÚMERO DE UN DÍGITO.
					JNE LECTNUM3
					POP AX
					SUB AX, 30H			;CONVERSIÓN DE ASCCI A DECIMAL.
					JMP SALIR			;RESULTADO GUARDADO EN AX.
				LECTNUM3:
					CMP N, 02H			;PARA UN NÚMERO DE DOS DÍGITOS.
					JNE SALIR
					POP AX
					SUB AX, 30H
					MOV BX, AX			;BX GUARDA EL DÍGITO DOS.
					POP AX
					SUB AX, 30H
					MOV CX, J
					MUL CX				;MULTIPLICA EL DÍGITO UNO POR 10^1 Y EL RESULTADO SE GUARDA EN DX/AX.
					ADD AX, BX			;SE SUMAN EL DÍGITO UNO CON EL DOS Y EL RESULTADO SE GUARDA EN AX.
				SALIR:
					RET
				LECTURA_NUMERO ENDP
	
				;SUBRUTINA PARA EL SALTO DE LÍNEA
				SALTOLINEA PROC
					MOV AH,02H 
					MOV DL,0AH 			;SALTO DE LÍNEA
					INT 21H
					MOV AH,02H 
					MOV DL,0DH 			;RETORNO DE CARRO
					INT 21H
					RET
				SALTOLINEA ENDP
				
				;SUBRUTINA PARA IMPRIMIR EL RESULTADO:
				IMPRIMIR_DECIMAL PROC
					MOV BX, 0AH			;EN BX SE ALMACENA EL DIVISOR A.
				IMPDECI2:
					CMP AX, 0H			;COMPRUEBA SI EL DIVIDENDO (AX) HA LLEGADO A CERO, SI ES CERO ENTONCES SALTA A IMPRIMIR.
					JE IMPDECI1
					MOV DX, 0H			;SE HACE USO DE LA OPERACIÓN AX=DX:AX/OP, PARA ESO DX DEBE RELLENARSE DE CEROS.
					DIV BX
					PUSH DX				;SE PONE EL RESIDUO DE LA DIVISIÓN EN LA PILA.
					INC I
					JMP IMPDECI2
				IMPDECI1:
					LEA DX, MENSAJE2
					MOV AH, 09H
					INT 21H
				IMPDECI3:
					CMP I, 0H
					JE FIN_IMPRIMIR
					POP DX				;SE SACA DE LA PILA EL RESIDUO QUE SE INGRESÓ.
					ADD DX, 30H			;SE SUMA 30H PARA HACER LA CONVERSIÓN DE DECIMAL A ASCII.
					MOV AH, 02H			;SE IMPRIME EL DÍGITO.
					INT 21H
					DEC I
					JMP IMPDECI3
				FIN_IMPRIMIR:
					RET
				IMPRIMIR_DECIMAL ENDP
CODIGO ENDS

PILA SEGMENT STACK
	DW 32 DUP (0)
PILA ENDS

DATOS SEGMENT
;POTENCIAS DE 10:
J DW 10
;CONTADORES:
N DB 0H						;DETERMINA EL TIPO DE NÚMERO A PARTIR DE SUS DÍGITOS.
I DW 0H						;CONTADOR DE LA SUBRUTINA IMPRIMIR_DECIMAL.
MENSAJE DB "INGRESE N: $"
MENSAJE2 DB "EL RESULTADO ES: $"
DATOS ENDS

END
